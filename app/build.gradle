plugins {
    id 'groovy'
}

configurations {
    oldApi
    newApi
}

dependencies {
    // Use the latest Groovy version for building this library
    implementation 'org.codehaus.groovy:groovy-all:3.0.7'

    // This dependency is used by the application.
    implementation 'com.google.guava:guava:30.1-jre'

    // Use the awesome Spock testing and specification framework even with Java
    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
    testImplementation 'junit:junit:4.13.2'

    testImplementation(project(":old-client")) {
        exclude(module: 'old-api')
    }

    oldApi(project(':old-api'))
    newApi(project(':new-api'))
}

tasks.named('test') {
    useJUnitPlatform()
    classpath += configurations.oldApi
}

tasks.register('apiEvolutionTest', Test) {
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath + configurations.newApi
    useJUnitPlatform()
}
